#
# zpath - Sets PATH related variables.
#

# Automatically remove duplicates from these arrays.
typeset -U path cdpath fpath manpath

# System $PATH.
path=(
  /usr/local/bin
  /usr/bin
  /bin
  /usr/local/sbin
  /usr/sbin
  /sbin
  /usr/X11R6/bin
  /usr/ucb            # SunOS
  /usr/sww/bin        # SunOS
  /usr/local/aws/bin
)

# Man pages.
manpath=(
  /usr/local/man
  /usr/man
)


# OS-specific paths.
case $OSTYPE in
  darwin*)
    # MacTeX binaries.
    [[ -d /usr/texbin ]] && path=( $path /usr/texbin )
    [[ -d /Library/TeX/texbin ]] && path=( $path /Library/Tex/texbin )
    # Apple's X11 binaries.
    [[ -d /usr/X11R6/bin ]] && path=( $path /usr/X11R6/bin )
    # Default MacPorts installations.
    if [[ -d /opt/local ]]; then
      path=( /opt/local/bin /opt/local/sbin $path )
      manpath=( /opt/local/share/man $manpath )
    fi
    if [[ -d /Users/$USER/Library/Android/sdk ]]; then
      export ANDROID_HOME=/Users/$USER/Library/Android/sdk
      path=($ANDROID_HOME/emulator $ANDROID_HOME/platform-tools $ANDROID_HOME/tools PATH=$ANDROID_HOME/tools/bin $path)
    fi
    # Common Homebrew paths
    if [[ -d /usr/local/opt/ruby/bin ]]; then
      path=(/usr/local/opt/ruby/bin $path)
    fi
    if [[ -d /usr/local/opt/avr-gcc@8/bin ]]; then
      path=(/usr/local/opt/avr-gcc@8/bin $path)
    fi
    if [[ -d /usr/local/Caskroom/google-cloud-sdk ]]; then
      source /usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/completion.zsh.inc
      source /usr/local/Caskroom/google-cloud-sdk/latest/google-cloud-sdk/path.zsh.inc
    fi
    ;;
esac

# Added for rust
fpath+=~/.zfunc

# Source local paths.
if [[ -f ~/.zpath.local ]]; then
  source ~/.zpath.local
fi

# Home prefix has highest precedence, thus sourced last.
path=( ~/bin $path )
manpath=( ~/man $manpath )
if [[ -d ~/.gem && -d ~/.gem/ruby ]]; then
  foreach dir (~/.gem/ruby/*) { [[ -d $dir/bin ]] && path=( $dir/bin $path ) }
fi


# Removes non-existent directories from an array.
clean-path () {
  local element
  local build
  build=()
  # Make sure that this works even with variables containing IFS characters,
  # if you're crazy enough to setopt shwordsplit.
  eval '
  foreach element in "$'"$1"'[@]"; do
      [[ -d "$element" ]] && build=("$build[@]" "$element")
  done
  '"$1"'=( "$build[@]" )
  '
}

# Finally, clean the path variables.
clean-path path
clean-path manpath
clean-path fpath

# vim: ft=zsh
